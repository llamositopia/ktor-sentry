buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "maven-publish"
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
    id "org.jlleitschuh.gradle.ktlint-idea" version "9.2.1"
}

group 'me.gitlab.llamos'
version '1.0.2'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            url "https://gitlab.com/api/v4/projects/19245109/packages/maven"
            name "GitLab"
            credentials(HttpHeaderCredentials) {
                name = "Job-Token"
                value = System.getenv("CI_JOB_TOKEN")
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
    publications {
        mavenJar(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    api "io.sentry:sentry:1.7.30"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
